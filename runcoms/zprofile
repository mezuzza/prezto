#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ -z "$BROWSER" && "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if [[ -z "$EDITOR" ]]; then
  export EDITOR='vim'
fi
if [[ -z "$VISUAL" ]]; then
  export VISUAL='vim'
fi
if [[ -z "$PAGER" ]]; then
  export PAGER='less'
fi

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

homebrew_locs=(
    /opt/homebrew/bin/brew
    /home/linuxbrew/.linuxbrew/bin/brew
)

for brewpath in "$homebrew_locs[@]"; do
    if [[ ! -x $brewpath ]]; then
        continue
    fi

    eval $($brewpath shellenv)

	# Forcibly set zsh site-functions. Don't know why, but fpath doesn't seem to
	# get populated without this.
	fpath[1,0]="$HOMEBREW_PREFIX/share/zsh/site-functions"

    break;
done

# Set the list of directories that Zsh searches for programs.
path=(
  $HOME/.local/bin(N-/)
  $HOME/go/bin(N-/)
  $HOME/.bun/bin(N-/)
  $HOME/.cargo/bin(N-/)
  $HOME/.ghcup/bin(N-/)
  $HOME/.krew/bin(N-/)
  $HOME/.yarn/bin(N-/)

  $HOME/.antigravity/antigravity/bin(N-/)

  $HOMEBREW_PREFIX/opt/gnu-getopt/bin(N-/)
  $HOMEBREW_PREFIX/opt/coreutils/libexec/gnubin(N-/)
  $HOMEBREW_PREFIX/opt/rustup/bin

  /usr/local/{,s}bin(N-/)

  $path
)

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X to enable it.
if [[ -z "$LESS" ]]; then
  export LESS='-g -i -M -R -S -w -z-4'
fi

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if [[ -z "$LESSOPEN" ]] && (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi
